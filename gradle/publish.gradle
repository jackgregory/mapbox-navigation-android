apply plugin: 'maven-publish'
apply plugin: 'com.mapbox.sdkRegistry'
apply from: file("${rootDir}/gradle/artifact-settings.gradle")
apply from: "${rootDir}/gradle/kdoc-settings.gradle"

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId project.ext.mapboxArtifactGroupId
                artifactId project.ext.mapboxArtifactId
                version project.ext.versionName

                artifact(androidSourcesJar)
                artifact(androidKdocJar)

                pom.withXml {
                    final mainNode = asNode()
                    mainNode.appendNode('name', project.ext.mapboxArtifactTitle)
                    mainNode.appendNode('description', project.ext.mapboxArtifactDescription)
                    mainNode.appendNode('url', project.ext.mapboxArtifactUrl)

                    final licenseNode = mainNode.appendNode('licenses').appendNode('license')
                    licenseNode.appendNode('name', project.ext.mapboxArtifactLicenseName)
                    licenseNode.appendNode('url', project.ext.mapboxArtifactLicenseUrl)
                    licenseNode.appendNode('distribution', "repo")

                    final developerNode = mainNode.appendNode('developers').appendNode('developer')
                    developerNode.appendNode('id', project.ext.mapboxDeveloperId)
                    developerNode.appendNode('name', project.ext.mapboxDeveloperName)

                    final scmNode = mainNode.appendNode("scm")
                    scmNode.appendNode("connection", project.ext.mapboxArtifactScmUrl)
                    scmNode.appendNode("developerConnection", project.ext.mapboxArtifactScmUrl)
                    scmNode.appendNode("url", project.ext.mapboxArtifactUrl)
                }
            }
            debug(MavenPublication) {
                from components.debug
                groupId project.ext.mapboxArtifactGroupId
                artifactId project.ext.mapboxArtifactId
                version project.ext.versionName

                artifact(androidSourcesJar)
                artifact(androidKdocJar)
            }
        }
    }
}

def sdkNameMap = [:]
sdkNameMap["libnavigation-base"] = "mobile-navigation-base-v2"
sdkNameMap["libnavigation-core"] = "mobile-navigation-core-v2"
sdkNameMap["libnavigation-metrics"] = "mobile-navigation-metrics-v2"
sdkNameMap["libnavigator"] = "mobile-navigation-navigator-v2"
sdkNameMap["libtrip-notification"] = "mobile-navigation-notification-v2"
sdkNameMap["libnavigation-router"] = "mobile-navigation-router-v2"
sdkNameMap["libnavigation-util"] = "mobile-navigation-utils-v2"
sdkNameMap["libnavui-base"] = "mobile-navigation-ui-base-v2"
sdkNameMap["libnavui-maps"] = "mobile-navigation-ui-maps-v2"
sdkNameMap["libnavui-util"] = "mobile-navigation-ui-utils-v2"
sdkNameMap["libnavui-tripprogress"] = "mobile-navigation-ui-tripprogress-v2"
sdkNameMap["libnavui-voice"] = "mobile-navigation-ui-voice-v2"

registry {
    sdkName = sdkNameMap[project.name]
    production = true
    snapshot = project.ext.versionName.endsWith("-SNAPSHOT")
    override = snapshot ? true : false
    dryRun = false
    publish = true
    publications = ["release"]
    featureFlag = "navigation_sdks_private_beta"
}

task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.kotlin
}

task androidKdocJar(type: Jar) {
    classifier = 'javadoc'
    from kdocPath
}
